plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.kotlin.kapt'
}

android {
    namespace 'ir.mahdiparastesh.sexbook'
    compileSdk 34
    buildToolsVersion = System.getenv("ANDROID_BUILD_TOOLS_VERSION")

    signingConfigs {
        main {
            storeFile file("${System.getenv("JKS_PATH")}")
            storePassword "${System.getenv("JKS_PASS")}"
            keyAlias 'sexbook'
            keyPassword "${System.getenv("JKS_PASS")}"
        }
    }

    defaultConfig {
        applicationId 'ir.mahdiparastesh.sexbook'
        minSdk 26
        targetSdk 34
        versionCode 43
        versionName '26.1.9'
        signingConfig signingConfigs.main
    }
    sourceSets.main {
        manifest.srcFile 'src/AndroidManifest.xml'
        java.srcDirs = ['src/java']
        kotlin.srcDirs = ['src/kotlin']
        res.srcDirs = ['src/res']
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_21; targetCompatibility JavaVersion.VERSION_21
    }
    kotlinOptions { jvmTarget = '21' }

    buildFeatures { buildConfig true; viewBinding true }
    buildTypes {
        debuggee {
            debuggable true
            minifyEnabled false
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    //noinspection GrDeprecatedAPIUsage
    variantFilter { if (it.buildType.name in ['debug', 'androidTest']) it.setIgnore(true) }
}
/*androidComponents.beforeVariants { variantBuilder ->
    if (variantBuilder.buildType in ['debug', 'androidTest'])
        variantBuilder.enable = false
}*/

dependencies {
    def room_version = '2.6.1'

    implementation 'androidx.activity:activity-ktx:1.9.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.drawerlayout:drawerlayout:1.2.0'
    implementation 'androidx.recyclerview:recyclerview:1.3.2'
    //noinspection KaptUsageInsteadOfKsp
    kapt "androidx.room:room-compiler:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    implementation "androidx.room:room-runtime:$room_version"
    implementation 'androidx.sqlite:sqlite-ktx:2.4.0'
    // implementation 'com.google.android.gms:play-services-ads:21.1.0'
    implementation 'com.google.android.material:material:1.12.0'
    implementation 'com.google.code.gson:gson:2.10.1'
    implementation 'ir.mahdiparastesh:hellocharts:2.6.5'
    implementation 'ir.mahdiparastesh:mcdtp:3.0'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.7.3'
}
